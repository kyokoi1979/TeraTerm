;; ホストリストが存在しない場合はダイアログから選択
filesearch HOST_FILE
if result = 0 then
  filenamebox 'ホストリストを選択してください' 0
  HOST_FILE = inputstr
  fileopen HOST_LIST HOST_FILE 0
else
  fileopen HOST_LIST HOST_FILE 0
endif

;; ホストリストの行数を取得する。
i = 0
while
  filereadln HOST_LIST HOST_LIST_LINE
  if result=1 then
    break
  endif
  i=i+1
endwhile
fileclose HOST_LIST

;; 配列を作成
strdim HOST_LIST_NAME i
strdim HOST_LIST_IP_1 i
strdim HOST_LIST_IP_2 i
strdim HOST_LIST_OS_1 i
strdim HOST_LIST_OS_2 i
strdim HOST_LIST_USER_1 i
strdim HOST_LIST_USER_2 i
strdim HOST_LIST_PASS_1 i
strdim HOST_LIST_PASS_2 i

;; ホストリストを読み込む
fileopen HOST_LIST HOST_FILE 0
i = 0
while
  filereadln HOST_LIST HOST_LIST_LINE
  if result=1 then
    break
  endif
 
; ホストリストから情報を取得する
  strmatch HOST_LIST_LINE '^#|^\t'
  if result<>1 then
    strsplit HOST_LIST_LINE '	'
    HOST_LIST_NAME[i]    = Groupmatchstr1
    HOST_LIST_IP_1[i]    = Groupmatchstr2
    HOST_LIST_OS_1[i]    = Groupmatchstr3
    HOST_LIST_USER_1[i]  = Groupmatchstr4
    HOST_LIST_PASS_1[i]  = Groupmatchstr5
    HOST_LIST_IP_2[i]    = Groupmatchstr6
    HOST_LIST_OS_2[i]    = Groupmatchstr7
    HOST_LIST_USER_2[i]  = Groupmatchstr8
    HOST_LIST_PASS_2[i]  = Groupmatchstr9
    
    i=i+1
  endif
endwhile
 
;; ホスト選択画面を表示させる
listbox 'ログインするホストを選択' 'Select HOST' HOST_LIST_NAME
if result >= 0 then
  HOST_NAME   = HOST_LIST_NAME[result]
  HOST_IP_1   = HOST_LIST_IP_1[result]
  HOST_OS_1   = HOST_LIST_OS_1[result]
  HOST_USER_1 = HOST_LIST_USER_1[result]
  HOST_PASS_1 = HOST_LIST_PASS_1[result]
  HOST_IP_2   = HOST_LIST_IP_2[result]
  HOST_OS_2   = HOST_LIST_OS_2[result]
  HOST_USER_2 = HOST_LIST_USER_2[result]
  HOST_PASS_2 = HOST_LIST_PASS_2[result]

  ;; OSに応じて認証方法を変更する
  strmatch HOST_OS_1 'Linux'
  if result > 0 then
    PROMPT = Linux_PROMPT
    AUTHTYPE = Linux_AUTHTYPE
  endif
  strmatch HOST_OS_1 'ESX'
  if result > 0 then
    PROMPT = ESX_PROMPT
    AUTHTYPE = ESX_AUTHTYPE
  endif

else
  end
endif

;; SSH接続用のコマンドを生成し、接続する。
sprintf2 COMMAND '%s /ssh /2 /nosecuritywarning /auth=%s /user=%s /passwd=%s' HOST_IP_1 AUTHTYPE HOST_USER_1 HOST_PASS_1
connect COMMAND

;; TERATERM.INIを開き、ターミナルのタイトルを変更する。
restoresetup INI_FILE
settitle HOST_NAME

;; ログの記録を行う
gettime Log_Time '%H%M%S'
sprintf2 Log_File '%s\%s_%s_%s_%s.log' Log_Path HOST_NAME Log_Date Log_Time Log_Str

;; 開いているログがあれば閉じて指定したファイル名でオープン
logclose
logopen Log_File 0 1 1 1 1

strmatch HOST_OS_1 'Linux'
if result > 0 then

  waitregex PROMPT
  sendln 'uname -a'

  waitregex PROMPT
  sendln 'id'

  waitregex PROMPT
  sendln 'date'

  waitregex PROMPT
  sendln 'df -h'

  waitregex PROMPT
  sendln 'w'

  waitregex PROMPT
  sendln 'ps auxww'

endif

strmatch HOST_OS_1 'ESX'
if result > 0 then
  waitregex PROMPT
  sendln 'uname -a'

  waitregex PROMPT
  sendln 'date'

  waitregex PROMPT
  sendln 'df -h'

  waitregex PROMPT
  sendln 'who'
endif

; IP②が入力されている場合はSSHログインを実行する。
strlen HOST_IP_2
if result > 0 then
  strmatch HOST_OS_2 'Linux'
  if result > 0 then
    PROMPT = Linux_PROMPT
  else
    PROMPT = ESX_PROMPT
  endif
  messagebox PROMPT ''
  sprintf2 CMD 'ssh -o "StrictHostKeyChecking no" %s@%s' HOST_USER_2 HOST_IP_2
  sendln CMD
  waitregex 'Password' 'password' 'パスワード'
  sendln HOST_PASS_2
  waitregex PROMPT
  sendln
endif

; コマンドリストを読み込み実行する
;; コマンドリストが存在しない場合はダイアログから選択
;; 踏み台の有無でコマンドリストを変更する
strlen HOST_OS_2
if result > 0 then
  strmatch HOST_OS_2 'Linux'
  if result > 0 then
    PROMPT = Linux_PROMPT
  else
    PROMPT = ESX_PROMPT
  endif
  sprintf2 Cmd_List_File '%s_%s.list' Cmd_Path HOST_OS_2
else
  sprintf2 Cmd_List_File '%s_%s.list' Cmd_Path HOST_OS_1
endif 

filesearch Cmd_List_File
if result = 0 then
  filenamebox 'コマンドリストを選択してください' 0
  Cmd_List = inputstr
endif

fileopen Cmd_List Cmd_List_File 0

while 1
  filereadln Cmd_List Cmd_List_Line
  if result = 1 break

  sendln
  flushrecv
  waitregex PROMPT
  sendln Cmd_List_Line
endwhile
fileclose Cmd_List

messagebox 'マクロの実行が完了しました' ''

end