;; ログイン用マクロ.ttl
INI       = 'TERATERM.INI'
TIMEOUT   = 60

;; 認証方法
;; password  ⇒ パスワード
;; challenge ⇒ チャレンジレスポンス/キーボードインタラクティブ
Linux_AUTHTYPE = 'password'
ESX_AUTHTYPE   = 'challenge'

;; 各OSのプロンプトを定義
Linux_PROMPT = '.*\][$|#]'
ESX_PROMPT   = '\[.*\] '

;; リストファイルはマクロと同じディレクトリから取得
getdate Log_Date  '%Y%m%d'
getdir Current_Dir
sprintf2 Cmd_Path '%s\cmdlist\command' Current_Dir
sprintf2 Log_Path '%s\log\%s' Current_Dir Log_Date

HOST_FILE = Current_Dir
HOST      = 'host.list'
makepath  HOST_FILE Current_Dir HOST
makepath  INI_FILE  Current_Dir INI

;; ログディレクトリ作成
;; ディレクトリを作成できない場合、同名のファイルが存在する場合は終了
filesearch Log_Path
if result=1 then
  foldersearch Log_Path
  if result <> 1 then
    sprintf2 msg '%s is a file.' Log_Path
    messagebox msg ''
    end
  endif
else
  foldercreate Log_Path

  if result <> 0 then
    messagebox 'ログディレクトリを作成できません。' ''
    end
  endif
endif

inputbox '"作業名"を入力して下さい。' '"作業名"'
Log_Str = inputstr

;; 一括or個別ログインの選択
strdim lst 2
lst[0] = '個別'
lst[1] = 'まとめて'
listbox 'ログイン方法を選択してください。' 'ログイン方法' lst
if result = -1 then ;; キャンセル
  end
elseif result = 0 then
  mode = 1
elseif result = 1 then
  mode = 2
endif

if mode = 1 then
  include 'sub/個別.ttl'
elseif mode = 2 then
  include 'sub/一括.ttl'
endif
